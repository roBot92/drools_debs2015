//created on: 2017.03.29.
package onlab.event

global onlab.utility.FrequentRoutesToplistSet mostFrequentRoutes
//list any import classes here.

import onlab.positioning.*

//declare any global variables here
declare TaxiLog
@role(event)
@timestamp(dropoff_datetime)
//@expires( 32m )
end

declare Tick
@role(event)
@timestamp(currentTime)
@expires( 1s )
end

declare RefreshRoute
@role(event)
@timestamp(currentTime)
end 

rule "refreshRoute"
salience 100
	when
		RefreshRoute($pickupCell:pickup_cell , $dropoffCell:dropoff_cell )
		TaxiLog(!mostFrequentRoutes.containsRoute($pickupCell, $dropoffCell), $refPickupCell: pickup_cell, $refDropoffCell: dropoff_cell)
		$countOfRoutes : java.lang.Number(doubleValue > 0) from accumulate 
        			( TaxiLog( pickup_cell == $refPickupCell , dropoff_cell == $refDropoffCell, pickup_cell  != null, dropoff_cell != null) over window:time(30m),
        			count()
        			)
        $maxDate : java.lang.Number() from accumulate 
        			( TaxiLog( pickup_cell == $refPickupCell , dropoff_cell == $refDropoffCell, pickup_cell  != null, dropoff_cell != null, $dropoffDate: dropoff_datetime) over window:time(30m),
        			 max($dropoffDate.getTime())
        			)
	then
	
	
		mostFrequentRoutes.add(new Route($refPickupCell, $refDropoffCell, new java.util.Date($maxDate.longValue()), (java.lang.Long) $countOfRoutes));
		
	
end




rule "TaxiLog_rule"

    when
        $tlog: TaxiLog(!processed,$pickupCell : pickup_cell , $dropoffCell : dropoff_cell , $dropoffTime : dropoff_datetime)
        $countOfRoutes : java.lang.Number(doubleValue > 0) from accumulate 
        			( TaxiLog( pickup_cell == $pickupCell , dropoff_cell == $dropoffCell, pickup_cell  != null, dropoff_cell != null) over window:time(30m),
        			count()
        			)
    then
    
    	
    	 $tlog.setProcessed(true); 
    	 update($tlog);
    	
    	
    	mostFrequentRoutes.add(new Route($pickupCell, $dropoffCell, $dropoffTime, (java.lang.Long) $countOfRoutes));
  
       
        

end

rule "TaxiLog_decrease"
    salience 50
    when
        $tick:Tick($currentTime : currentTime )
        $tlog:TaxiLog((dropoff_datetime.getTime() + 30 * 60 * 1000) < $currentTime, $pickupCell: pickup_cell, $dropoffCell: dropoff_cell)
    then 
    
    	mostFrequentRoutes.decreaseRouteFrequency($pickupCell, $dropoffCell);
    	retract($tlog);	
     	insertLogical(new RefreshRoute($pickupCell, $dropoffCell, $currentTime));
        

end



