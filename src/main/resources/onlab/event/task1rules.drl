//created on: 2017.03.29.
package onlab.event




//list any import classes here.
//import accumulate MedianAccumulateFunction medianf
import onlab.positioning.*
import org.kie.api.runtime.rule.QueryResults
import java.math.BigDecimal
import java.util.ArrayList
import onlab.utility.CustomTreeMultiset



//declare any global variables here
global onlab.utility.ProfitableAreaToplistSet mostProfitableAreas
global onlab.utility.FrequentRoutesToplistSet mostFrequentRoutes



declare TaxiLog
@role(event)
@timestamp(dropoff_datetime)
//@expires( 32m )
end

declare Tick
@role(event)
@timestamp(currentTime)
@expires( 1s )
end

declare RefreshRoute
@role(event)
@timestamp(currentTime)
end 






declare RefreshArea
@role(event)
@timestamp(currentTime)
end

declare ActivateArea
@role(event)
end

declare ReOrderAreas
@role(event)
end

declare AreaWithProfit
@role(event)
@timestamp(lastInserted)
end





rule "task2_new_TaxiLog_rule" 
salience 0
    when
      $tlog: TaxiLog(!processed, $pickupCell: pickup_cell , $dropoffCell: dropoff_cell , $currentTime: dropoff_datetime , $inserted: inserted )
      
    then
      $tlog.setProcessed(true); 
      update($tlog);
      insertLogical(new ActivateArea($pickupCell, $currentTime));
      insertLogical(new RefreshArea($pickupCell,$currentTime, $inserted));  
      insert(new ReOrderAreas());

end



rule "task2_ActivateAreaRule"
salience 10
	when
		ActivateArea($cell : cell, $ctime: currentTime) 
		not AreaWithProfit($cell == cell)
	then
		insert(new AreaWithProfit($cell, $ctime)); 
end




rule "task2_refreshAreaRule"
salience 20
	when
		RefreshArea($ctime: currentTime, $cell:cell, $insertedForDelay : insertedForDelay )
		$area:AreaWithProfit($cell == cell, lastInserted.getTime()+15*60*1000 >= $ctime.getTime())
		/*pickup_datetime.getTime()+15*60*1000 >= $ctime.getTime()*/
		$median : java.math.BigDecimal() from accumulate 
        			( TaxiLog($fare: fare_amount , $tip:tip_amount , pickup_cell == $cell) over window:time(15m),
        			init(CustomTreeMultiset list = new CustomTreeMultiset();),
        			action(
        				list.add($fare.add($tip));
        			),
        			reverse(list.remove($fare.add($tip));),
        			result(list.getMedian())
        			)
        $taxiListOfArea: java.util.ArrayList() from collect (TaxiLog(dropoff_cell == $cell) over window:time(30m)) 
        $lastInserted: java.lang.Number() from accumulate(TaxiLog($dropoffTime : dropoff_datetime ,pickup_cell == $cell),
        												max($dropoffTime.getTime())
        												)
	then	
	
		int $numOfEmptyTaxis= 0;
		for(Object $o : $taxiListOfArea){
			TaxiLog $tlog = (TaxiLog)$o;
			QueryResults results =
    		kcontext.getKnowledgeRuntime().getQueryResults( "task2_newerRidesOfDriver", new Object[]{$tlog.getHack_license(), $tlog.getDropoff_datetime()} );
    		if(results.size() == 0 ){
    			$numOfEmptyTaxis++;
    		}
		}
		
		BigDecimal $medianProfit = ((BigDecimal)$median).divide(($numOfEmptyTaxis == 0 ? BigDecimal.ONE : BigDecimal.valueOf($numOfEmptyTaxis)),2, BigDecimal.ROUND_HALF_UP);
		if(!$medianProfit.equals($area.getMedianProfit())){
			$area.setMedianProfit($medianProfit);
			$area.setLastInserted(new java.util.Date($lastInserted.longValue()));
			$area.setInsertedForDelay($insertedForDelay);
			$area.setDelay(0);
			update($area);
		}
		
end



rule "task2_deactivateArea"
salience 10
	when
		RefreshArea($ctime: currentTime, $cell:cell )
		$area:AreaWithProfit(lastInserted.getTime() + 15 * 60 * 1000 < $ctime.getTime())
	then
		retract($area);
end			



rule "task2_remove_TaxiLog_from_pickupCell"
salience 9
    when
        $tick:Tick($currentTime : currentTime, $insertedForDelay: inserted  )
        $tlog:TaxiLog(dropoff_datetime.getTime() + 15 * 60 * 1000 < $currentTime, !olderThanAQuarter, $pickupCell : pickup_cell)
    then
    	$tlog.setOlderThanAQuarter(true);
    	update($tlog);
    	insertLogical(new RefreshArea($pickupCell, new java.util.Date($currentTime) , $insertedForDelay));
  		insert(new ReOrderAreas());
        

end

rule "task2_remove_TaxiLog_from_WM"
salience 4
    when
        $tick:Tick($currentTime : currentTime, $insertedForDelay : inserted  )
        $tlog:TaxiLog(dropoff_datetime.getTime() + 30 * 60 * 1000 < $currentTime, $dropoffCell : dropoff_cell)
    then
    	retract($tlog);
    	insertLogical(new RefreshArea($dropoffCell, new java.util.Date($currentTime) , $insertedForDelay));
      	insert(new ReOrderAreas());
        

end

rule "task2_ReorderAreas_rule"
salience -1
	when
		ReOrderAreas()
		$allReorderCollection: java.util.ArrayList() from collect (ReOrderAreas())
		$areas:  java.util.ArrayList() from collect( AreaWithProfit() )
	then
		mostProfitableAreas.clear();
		if($areas != null){
		mostProfitableAreas.addAll($areas);
		}
		for(Object $reorder : $allReorderCollection ){
			retract($reorder);
		}
		
end	

rule "task2_NoAreas"
salience -1
	when
		ReOrderAreas()
		not AreaWithProfit()
	then
		mostProfitableAreas.clear();
		
end	







query "task2_newerRidesOfDriver"  (String $license, java.util.Date $threshold)

      tlog:TaxiLog(hack_license.equals($license) , $threshold.before(dropoff_datetime) )
end

//For debug
query "areas"  ()

      areas:AreaWithProfit()
end

query "taxis"()
	taxis: TaxiLog()
end

